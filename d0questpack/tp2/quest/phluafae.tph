COPY ~%MOD_FOLDER%/phluafae/suinvis.cre~ ~override~
COPY ~%MOD_FOLDER%/phluafae/sumound.cre~ ~override~

COPY ~%MOD_FOLDER%/phluafae/sudryad.cre~ ~override~
  SAY NAME1 @135
  SAY NAME2 @135
  WRITE_ASCII 0x34 "" #8

COPY ~%MOD_FOLDER%/phluafae/sufake.itm~ ~override~
  SAY NAME1 @138
  SAY NAME2 @138
  SAY IDENTIFIED_DESC @139
  SAY UNIDENTIFIED_DESC @139

COPY ~%MOD_FOLDER%/phluafae/sutear.itm~ ~override~
  SAY NAME1 @140
  SAY NAME2 @141
  SAY UNIDENTIFIED_DESC @142
  SAY IDENTIFIED_DESC @143
  PATCH_FOR_EACH parameter1 IN 14672 14780 BEGIN
    LPF ADD_ITEM_EQEFFECT
      INT_VAR
        opcode = 267 // disable display string
        target = 1
        parameter1
        timing = 2
        resist_dispel = 2
    END
  END
  LPF DELETE_ITEM_EQEFFECT
    INT_VAR
      opcode_to_delete = 296
  END
  LPF ADD_ITEM_EQEFFECT
    INT_VAR
      opcode = 296 // protection from animation
      target = 1
      timing = 2
      resist_dispel = 2
    STR_VAR
      resource = spnwchrm
  END

COPY ~%MOD_FOLDER%/phluafae/sutear.bam~ ~override~
     ~%MOD_FOLDER%/phluafae/sufake.bam~ ~override~

COPY_EXISTING spin770.spl ~override/fl#qpchk.spl~
  PATCH_FOR_EACH opcode_to_delete IN 215 174 BEGIN
    LPF DELETE_SPELL_EFFECT
      INT_VAR
        opcode_to_delete
    END
  END

EXTEND_TOP ~AR1300.bcs~ ~%MOD_FOLDER%/phluafae/appendAR1300.baf~ EVALUATE_BUFFER
EXTEND_TOP ~AR1304.bcs~ ~%MOD_FOLDER%/phluafae/appendAR1300.baf~ EVALUATE_BUFFER
EXTEND_TOP ~AR1200.bcs~ ~%MOD_FOLDER%/phluafae/appendAR1200.baf~ EVALUATE_BUFFER
EXTEND_TOP ~baldur.bcs~ ~%MOD_FOLDER%/phluafae/baldurappend.baf~ EVALUATE_BUFFER
EXTEND_TOP ~VAELASA.bcs~ ~%MOD_FOLDER%/phluafae/appendvae.baf~ EVALUATE_BUFFER

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/phluafae/SUFAKEA.baf~
        ~%MOD_FOLDER%/phluafae/SUFAKEB.baf~

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/phluafae/SUINVIS.baf~
        ~%MOD_FOLDER%/phluafae/SUMOUND.baf~

ACTION_IF !FILE_EXISTS_IN_GAME j#klsyj.dlg BEGIN
  COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/phluafae/dialog.d~ USING
          ~%tra_path%/english/phluafae.tra~
          ~%tra_path%/%LANGUAGE%/phluafae.tra~
END

/*
 * Need to be compiled together due to label references
 */
ACTION_IF FILE_EXISTS_IN_GAME j#klsyj.dlg BEGIN
  COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/phluafae/dialog.d~
          ~%MOD_FOLDER%/phluafae/kelsey.d~ USING
          ~%tra_path%/english/phluafae.tra~
          ~%tra_path%/%LANGUAGE%/phluafae.tra~
END

ADD_JOURNAL EXISTING TITLE (#74415) @137 @138 @139 @140 @141 @142 USING
            ~%tra_path%/english/phluafae.tra~
            ~%tra_path%/%LANGUAGE%/phluafae.tra~

COMPILE EVALUATE_BUFFER ~%MOD_FOLDER%/phluafae/SUDRYAD.baf~
